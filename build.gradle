buildscript {
    repositories {
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id "net.neoforged.moddev.legacyforge" version "2.0.139"
    id "maven-publish"
}

base {
    archivesName = project.archives_base_name
}

version = project.mod_version
group = project.maven_group

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17
}

def generatedResources = file("src/generated")

sourceSets {
    main {
        resources.srcDir generatedResources
        ext.refMap = "${mod_id}.mixins.refmap.json"
    }
}

legacyForge {
    version = project.minecraft_version + '-' + project.forge_version

    runs {
        configureEach {
            gameDirectory = project.file('run')
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }

        client {
            client()
            sourceSet = sourceSets.main

            //uncomment for better debugging with the JBR JDK
            //jvmArgument "-XX:+AllowEnhancedClassRedefinition"
            systemProperty 'forge.enabledGameTestNamespaces', '${mod_id}'
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            systemProperty 'forge.logging.console.level', 'debug'
            systemProperty 'forge.enabledGameTestNamespaces', '${mod_id}'
            programArguments.addAll('--existing-mod', 'gtceu')
        }

        server {
            server()
            sourceSet = sourceSets.main

            systemProperty 'forge.enabledGameTestNamespaces', '${mod_id}'
            programArguments.addAll('--nogui', '--world', 'world-extra')
            programArgument "-mixin.config=${mod_id}.mixins.json"
        }

        data {
            data()

            sourceSet = sourceSets.main

            programArguments.addAll('--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath())
            programArguments.addAll('--existing-mod', 'gtceu')
        }
    }
    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

repositories {
    mavenLocal()
    flatDir {
        dir 'libs'
    }
    mavenCentral()
    maven {
        name = 'GTCEu Maven'
        url = 'https://maven.gtceu.com'
        content {
            includeGroup 'com.gregtechceu.gtceu'
        }
    }
    maven {
        name "firstdarkMavenSnapshots"
        url "https://maven.firstdark.dev/snapshots"
    }
    maven {
        name 'Quilt'
        url 'https://maven.quiltmc.org/repository/release/'
    }
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
    maven {
        // latvian Maven (KubeJS and Rhino)
        url = "https://maven.latvian.dev/releases"
        content {
            includeGroup "dev.latvian.mods"
            includeGroup "dev.latvian.apps"
        }
    }
    maven { // Registrate
        url = "https://maven.tterrag.com/"
        content {
            // need to be specific here due to version overlaps
            includeGroup("com.jozufozu.flywheel")
            includeGroup("com.tterrag.registrate")
            includeGroup("com.simibubi.create")
        }
    }
    maven {
        // Patchouli, JEI
        name = "BlameJared"
        url = "https://maven.blamejared.com/"
    }
    maven {
        url = "https://maven.theillusivec4.top/"
    }
    maven {
        // Curse Forge File
        url "https://cursemaven.com/"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        // EMI
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/"
    }
    maven {
        url = "https://modmaven.dev/"
        content {
            includeGroup "appeng"
        }
    }

    maven {
        url "https://maven.ftb.dev/releases"
    }

    maven { url "https://maven.covers1624.net" }
}

dependencies {
    implementation('io.reactivex:rxjava:1.3.8')
	//forgeRuntimeLibrary('io.reactivex:rxjava:1.3.8')
    jarJar('io.reactivex:rxjava:1.3.8')

    implementation('com.github.davidmoten:rtree:0.12')
    //forgeRuntimeLibrary('com.github.davidmoten:rtree:0.12')
    jarJar('com.github.davidmoten:rtree:0.12')

    implementation('com.github.davidmoten:guava-mini:0.1.7')
    //forgeRuntimeLibrary('com.github.davidmoten:guava-mini:0.1.7')
    jarJar('com.github.davidmoten:guava-mini:0.1.7')

    modCompileOnly("mezz.jei:jei-$project.minecraft_version-forge-api:$project.jei_version") { transitive = false }
    modCompileOnly("mezz.jei:jei-$project.minecraft_version-common-api:$project.jei_version") { transitive = false }

    modImplementation("curse.maven:architectury-api-419699:5137938")
    modImplementation("curse.maven:kubejs-238086:5853326")
    modImplementation("curse.maven:rhino-416294:6186971")

    modImplementation("dev.ftb.mods:ftb-library-forge:2001.2.9")
    modImplementation("dev.ftb.mods:ftb-teams-forge:2001.3.1")
    modImplementation("com.gregtechceu.gtceu:gtceu-$project.minecraft_version:$project.gtceu_version") { transitive = false }
    modImplementation("com.lowdragmc.ldlib:ldlib-forge-$project.minecraft_version:$project.ldlib_version") { transitive = false }
    modImplementation("com.tterrag.registrate:Registrate:$project.registrate_version")
    modImplementation("dev.latvian.mods:kubejs-forge:$project.kubejs_version")
    modImplementation("appeng:appliedenergistics2-forge:15.3.3")
    modImplementation("curse.maven:jade-324717:6106101")

    modRuntimeOnly("curse.maven:sgjourney-689083:6390662")
    modRuntimeOnly("curse.maven:thermal-extra-573674:6474572")
    modRuntimeOnly("com.teamcofh:thermal_core:1.20.1-11.0.7+")
    modRuntimeOnly("curse.maven:thermal-foundation-222880:5443583")
    modRuntimeOnly("curse.maven:thermal-cultivation-271835:5372746")
    modRuntimeOnly("curse.maven:thermal-expansion-69163:5372749")
    modRuntimeOnly("curse.maven:thermal-innovation-291737:5372751")
    modRuntimeOnly("curse.maven:cofh-core-69162:5374122")


    modRuntimeOnly("dev.toma.configuration:configuration-forge-$project.minecraft_version:$project.configuration_version")
    modRuntimeOnly("mezz.jei:jei-$project.minecraft_version-forge:$project.jei_version") { transitive = false }
    modRuntimeOnly("curse.maven:spark-361579:4738952")
    modRuntimeOnly ("dev.emi:emi-forge:$project.emi_version+$project.minecraft_version")

    // building gadgets -> bug checking
    modRuntimeOnly("curse.maven:building-gadgets-298187:6850495")

    // Mixin Stuff
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:$project.mixinextras_version"))
    implementation(jarJar("io.github.llamalad7:mixinextras-forge:$project.mixinextras_version"))

    // lombok
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
}

processResources {
    // set up properties for filling into metadata
    var properties = [
            "mod_license": mod_license,
            "mod_id": mod_id,
            "version": version,
            "mod_name": mod_name,
            "mod_url": mod_url,
            "mod_author": mod_author,
            "forge_version": forge_version.split("\\.")[0], // only specify major version of forge
            "minecraft_version": minecraft_version,
            "gtceu_version": gtceu_version
    ]
    inputs.properties(properties)

    filesMatching("META-INF/mods.toml") {
        expand properties
    }
}

tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    options.encoding = "UTF-8"
    options.release.set(17)
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    // add some additional metadata to the jar manifest
    manifest {
        attributes([
                "Specification-Title"     : mod_id,
                "Specification-Vendor"    : mod_author,
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : version,
                "Implementation-Vendor"   : mod_author,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    finalizedBy 'reobfJar'
}

mixin {
    add sourceSets.main, "${mod_id}.mixins.refmap.json"
    config "${mod_id}.mixins.json"
}

jar {
    manifest {
        attributes([
                "MixinConfigs": "${mod_id}.mixins.json"
        ])
    }
}